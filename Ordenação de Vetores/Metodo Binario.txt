prog metodobinario
     int index, num, dado,
          meio /*posição central tabela*/,
          start /*posição inicial da busca*/,
          end /*posição final da busca */, 
          vetor[30] /*tabela para os elementos */,
          ACHOU; /*valor lógico que representa o sucesso da busca*/    

     imprima "Digite o tamanho da tabela: ";
     leia num;

     imprima "\nInforme os dados da tabela: \n";
     para (index <- 0; index < num; index++){ 
          leia vetor[index]; 
     }
        
     imprima "\nValores Informados:";
     para (index <- 0; index < num; index++){ 
          imprima vetor[index], " ";
     }
 
     imprima"\nDigite o elemento a ser procurado: ";
     leia dado;
     ACHOU <- 0;    
     start <- 0;    
     end <- (num - 1);    
     enquanto ((start <= end) && (ACHOU == 0)) 
     {
          meio <- realint ((start + end) / 2);        #//divisao de inteiro, resulta inteiro
          se (vetor[meio] == dado){
               ACHOU <- 1;
          }                    
          senao{
               se (vetor[meio] > dado){
                    end <- (meio - 1);
               }      #// ajusta fim do vetor
               senao{
                    start <- (meio + 1); 
               }  #// ajusta inicio do vetor
          }
     }
     se (ACHOU == 1){
          imprima dado , " foi encontrado. "; 
     }senao{ 
          imprima dado, " NAO se encontra na TABELA."; 
     }
     imprima "\n";
  
fimprog